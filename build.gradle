plugins {
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.22'
    id "org.jlleitschuh.gradle.ktlint" version '12.0.3'
    id "org.openapi.generator" version "7.2.0"
}

ext {
    mybatisSpringBootStarterVersion = "3.0.3"
    springModulithBomVersion = "1.1.1"
    archunitVersion = '1.2.1'
    springdocVersion = '2.3.0'
    mockitoKotlinVersion = '5.2.1'
}

group = 'com.example'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

kotlin {
    jvmToolchain(17)
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //spring tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.modulith:spring-modulith-starter-core'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"


    //container
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

    //web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    //data
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:${mybatisSpringBootStarterVersion}"
    implementation 'org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.5.0'


    //monitor
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //test
    testImplementation 'org.testcontainers:mysql'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:${mybatisSpringBootStarterVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation "com.tngtech.archunit:archunit-junit5:${archunitVersion}"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${mockitoKotlinVersion}"

    //kotlin-module
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'

    //data-connector
    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    //lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithBomVersion}"
    }
}


test {
    useJUnitPlatform()
}

jar {
    archiveClassifier = ''
    enabled = false
}

openApiValidate {
    inputSpec.set("$rootDir/docs/swagger/specs/openApiMain.yaml")
}

//https://openapi-generator.tech/docs/generators/kotlin-spring/
openApiGenerate {
    generatorName.set("kotlin-spring")
    outputDir.set("$rootDir/build/generated")
    inputSpec.set("$rootDir/docs/swagger/specs/openApiMain.yaml")
    configOptions.set([
            additionalModelTypeAnnotations   : "null",
            annotationLibrary                : "swagger2",
            apiPackage                       : "com.example.kotlinonspringboot.presentation.api",
            apiSuffix                        : "Api",
            artifactId                       : "openapi-spring",
            artifactVersion                  : "1.0.0",
            basePackage                      : "com.example.kotlinonspringboot.presentation.openapitools",
            beanQualifiers                   : "false",
            configPackage                    : "com.example.kotlinonspringboot.presentation.config",
            delegatePattern                  : "false",
            documentationProvider            : "springdoc",
            enumPropertyNaming               : "camelCase",
            exceptionHandler                 : "false",
            gradleBuildFile                  : "false",
            groupId                          : "org.openapitools",
            interfaceOnly                    : "true",
            library                          : "spring-boot",
            modelMutable                     : "false",
            modelPackage                     : "com.example.kotlinonspringboot.presentation.model",
            packageName                      : "org.openapitools",
            parcelizeModels                  : "null",
            reactive                         : "false",
            serializableModel                : "null",
            serverPort                       : "8080",
            serviceImplementation            : "false",
            serviceInterface                 : "false",
            skipDefaultInterface             : "false",
            sortModelPropertiesByRequiredFlag: "null",
            sortParamsByRequiredFlag         : "null",
            sourceFolder                     : "src/main/kotlin",
            title                            : "OpenAPI Kotlin Spring",
            useBeanValidation                : "true",
            useFeignClientUrl                : "true",
            useSpringBoot3                   : "true",
            useSwaggerUI                     : "true",
            useTags                          : "true"
    ])
}

compileKotlin {
    dependsOn("openApiGenerate")
}

kotlin.sourceSets.main {
    kotlin.srcDir("${openApiGenerate.outputDir.get()}/src/main/kotlin")
}

ktlint {
    debug = true
    verbose = true

    filter {
        exclude { entry ->
            entry.file.toString().contains("generated")
        }
        include("**/kotlin/**")
    }
}
